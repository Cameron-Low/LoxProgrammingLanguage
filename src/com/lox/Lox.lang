program -> statement* EOF;

declaration -> classDecl | funDecl | varDecl | statement;

classDecl -> "class" IDENTIFIER ( "<" IDENTIFIER )? "{" function* "}"

funDecl -> "fun" function;

function -> IDENTIFIER? "(" parameters? ")" block;

parameters -> IDENTIFIER ( "," IDENTIFIER )*;

varDecl -> "var" IDENTIFIER ("=" expression )? ";";

statement -> exprStmt | ifStmt | whileStmt | forStmt | printStmt | returnStmt | block;

exprStmt -> expression ";";

ifStmt -> "if" "(" expression ")" statement ( "else" statement )?;

whileStmt -> "while" "(" expression ")" statement;

forStmt -> "for" "(" ( varDecl | exprStmt | ";" ) expression? ";" expression? ")" statement;

printStmt -> "print" expression ";";

returnStmt -> "return" expression? ";";

block -> "{" declaration* "}";

expression -> assignment;

assignment -> ( call "." )? IDENTIFIER "=" assignment | logic_or;

logic_or -> logic_and ( "or" logic_and )*;

logic_and -> equality ( "and" equality )*;

equality -> comparison ( ("!=" | "==") comparison)*;

comparison -> addition ( (">" | ">=" | "<" | "<=") addition)*;

addition -> multiplication ( ("+" | "-") multiplication)*;

multiplication -> unary ( ("*" | "/") unary)*;

unary -> ("!" | "-") unary | call;

call -> primary ( "(" arguments?  )" | "." IDENTIFIER )*;

arguments -> expression ( "," expression )*;

primary -> NUMBER | STRING |  "true" | "false" | "nil" | "this" | "super" "." IDENTIFIER | "(" expression ")" | IDENTIFIER;
